
NAME
   CONTINUATION(5f) - [FORTRAN] - the rules for line continuation

SYNOPSIS

   original statement

   becomes

   original&\n&statement

DESCRIPTION

You may split almost any free-format Fortran statement into multiple lines
by inserting the sequence "&\n&", where "\n" represents a newline. That is, 
split the line into two lines and place an ampersand at the right end of the
first line and as the first (non-space) character in the second line. 

So for example, the line

  integer,save :: xx(2,3)= reshape([ 1, 2, 3, 4, 5, 6 ],shape(xx),order=[2,1])

may be split into many lines by simple repeated application of the rule:

    integer,save :: xx(2,3)= reshape([&
    & 1, 2, 3,  &
    & 4, 5, 6   &
    &],shape(xx),order=[2,1])

That it basically it, but there are a few variants and details to cover.

When a line is split any leading spaces before the ampersand beginning
the second line are ignored. So you can indent the lines beginning with an
ampersand any way you like:

    integer,save :: xx(2,3)= reshape([&
       & 1, 2, 3,  &
       & 4, 5, 6   &
    &],shape(xx),order=[2,1])

COMMENTING CONTINUED LINES

Now there is the issue of whether comments are allowed after the trailing
ampersands. They are! So lets explain why we wanted this line to be split up:

    integer,save :: xx(2,3)= reshape([& ! define array in row-column order 
       & 1, 2, 3,  &                    ! row 1
       & 4, 5, 6   &                    ! row 2
    ],shape(xx),order=[2,1])

Continuation lines may be separated by blank or comment lines as well. So
the following is still equivalent to the first example line:

    integer,save :: xx(2,3)= reshape([& ! define array in row-column order 

       !===========!
       & 1, 2, 3,  &
       & 4, 5, 6   &
       !===========!

       ],shape(xx),order=[2,1])
   NOTE:

   This might be a good time to mention ampersands in a comment are
   ignored, so you cannot continue a comment onto another line, but
   must start an additional comment line if you want it to appear across
   multiple lines.

To summarize, the rule is simple for comments, really. They can go in
the same places on continued lines as on non-continued lines.

SOMETIMES OPTIONAL LEADING AMPERSANDS

If you want to make things a little prettier the leading ampersand on
the second line is optional if you are splitting where a non-significant
space would be allowed (we will get to the opposite issue - when the
second ampersand is required, next).

    integer,save :: xx(2,3)= reshape([& ! define array in row-column order 
         1, 2, 3,  &
         4, 5, 6   &
    ],shape(xx),order=[2,1])

YOU HAVE TO HAVE THE LEADING AMPERSANDS WHEN SPLITTING IN A BAD PLACE

But you have to have the leading amersand on continued lines when splitting
quoted strings or lexical words or values in the middle. So this line 

    character(len=*), parameter :: str1='my first str', str2='my second str'

could be written as
    char&
    &acter(len=*), para&
    &meter :: str1='my fi&
    &rst str', str2='my se&
    &cond str'

where things were split in two in a haphazard way as long as no spaces
are introduced before the ending amersand and after the leading amersand
that would make the statement illegal if all appearing on one line
(but try to never split constants or lexical words!).

   NB.: To reiterate, it is bad practice to split strings and words and
   constants in their middles onto two lines, with the possible exception
   of long string constants. 

This is a more realistic example (a very long string):

   character(len=*),parameter='this is a really long string &
     &that I needed to put onto several lines because it would be&
     & so long if I left it on a single line that it might be longer&
     & than allowed in older compilers and would certainly not fit &
     &in my favorite 80-column&
     & terminal window'

So lines can be broken within a string constant or lexical word or
constant, in which case the initiating ampersand on the second line is
mandatory and spaces preceeding the ampersand at the end of the first
line are significant (and retained if part of a split string).

HOW LONG YOU CAN CONTINUE

Since we are talking about very long lines, how long can a single
statement be? In the Fortran 95 standard, only a maximum of 39
continuation lines is required to be conformant. In Fortran 2003 and
Fortran 2008, at least 255 is to be allowed. There is no limit specified
in Fortran 2018. See your compiler documentation to see if your compiler
still has a limit, but it is probably at least a few hundred lines.

FIXED FORMAT AND INTERSECTION FORMAT INCLUDE FILES

Fixed-format Fortran has a very different continuation rule where the
first line has nothing added to it except an optional zero in column
six and all continuations have a non-space non-zero character that is
part of the Fortran character set in column six. If a quoted string is
broken the first line acts as if padded with spaces out to column 72.

Surprisingly, source code can be formatted in an “intersection”
format that works in both free and fixed-format files.

Other than a curiosity, an "intersection" file might be required for
an INCLUDE file that is needed by both free and fixed-format files.
(Note that INCLUDE statements themselves are one of the few statements
that cannot be split across multiple lines!).

So here is how to make an "intersection format" INCLUDE file:
   
   If every line being continued has an ampersand in column 73 or further
   the ampersand will be ignored by standard fixed-format Fortran and
   if the second amersand is always present and in column six both rules
   are satisfied.
   
So, to summarize "intersection" continued line format in an example,
the following line is equivalent in fixed and free-format parsing:
   
     12345 continue
           character(len=*), parameter :: string1="hello world", string2="hel&
          &lo world"
   
Obviously, this is not compatible with extended length fixed-format
source files unless the ampersand is shifted beyond the extended limit
(which in standard fixed-format files would be past column 72).

You may want to look for a compiler option to disable long-line warnings
when using "intersection format" INCLUDE files.

FORMAL RULES

So, to put it another way, here is part of what the f2018 standard draft
(J3/18-007r1WD 1539-12018-08-286.3.2.3) says:

  FREE FORM COMMENTARY
 
  The character “!” initiates a comment except where it appears
  within a character context. The comment extends to the end of the line.
  If the first nonblank character on a line is an “!”, the line
  is a comment line. Lines containing only blanks or containing no
  characters are also comment lines. Comments may appear anywhere in a
  program unit and may precede the first statement of a program unit or
  follow the last statement of a program unit. Comments have no effect
  on the interpretation of the program unit.

  NOTE 

  This document does not restrict the number of consecutive comment lines.


  FREE FORM STATEMENT CONTINUATION 

  Per (6.3.2.4):

  The character “&” is used to indicate that the statement is
  continued on the next line that is not a comment line. Comment lines
  cannot be continued; an “&” in a comment has no effect. Comments
  may occur within a continued statement. When used for continuation,
  the “&” is not part of the statement. No line shall contain a
  single “&” as the only nonblank character or as the only nonblank
  character before an “!” that initiates a comment.

  If a noncharacter context is to be continued, an “&” shall be the
  last nonblank character on the line, or the last nonblank character
  before an “!”. There shall be a later line that is not a comment;
  the statement is continued on the next such line. If the first nonblank
  character on that line is an “&”, the statement continues at the
  next character position following that “&”; otherwise, it continues
  with the first character position of that line.

  If a lexical token is split across the end of a line, the first nonblank
  character on the first following noncomment line shall be an “&”
  immediately followed by the successive characters of the split token.

  If a character context is to be continued, an “&” shall be the
  last nonblank character on the line. There shall be a later line that
  is not a comment; an “&” shall be the first nonblank character on
  the next such line and the statement continues with the next character
  following that “&”.

  SUMMARY

  Splitting a line with the sequence "&\n&" where "\n" represents a newline
  will continue a Fortran line onto two lines. Comments can go onto continued
  lines just like they do on other statements. The beginning ampersand on the
  second line is optional if you are not splitting a constant or lexical token
  or quoted string, otherwise it is required. Any modern compiler supports at
  least 39 continuation lines, and probably much more. Spaces before the leading
  amersand on the second part of the split line are ignored.

  JSU
