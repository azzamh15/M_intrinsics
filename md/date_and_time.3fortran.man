NAME
   date_and_time(3f) - [FORTRAN:INTRINSIC:SYSTEM ENVIRONMENT] gets current time

SYNTAX
    subroutine date_and_time([date, time, zone, values])

     character(len=8),intent(out) :: date
     character(len=10),intent(out) :: time
     character(len=5),intent(out) :: zone
     integer,intent(out) :: values

DESCRIPTION
   DATE_AND_TIME(date, time, zone, values) gets the corresponding date
   and time information from the real-time system clock.

   Unavailable time and date parameters return blanks.

ARGUMENTS
   DATE      The type shall be character(len=8) or larger,
             and of default kind.  DATE has form ccyymmdd.
   TIME      The type shall be character(len=10) or
             larger, and of default kind.  TIME has form hhmmss.sss.
   ZONE      The type shall be character(len=5) or larger,
             and of default kind.  ZONE has form (+-)hhmm, representing
             the difference
             with respect to Coordinated Universal Time (UTC). 
   VALUES    The type shall be integer(8). VALUES provide the following:

               * value(1): - The year
               * value(2): - The month
               * value(3): - The day of the month
               * value(4): - Time difference with UTC in minutes
               * value(5): - The hour of the day
               * value(6): - The minutes of the hour
               * value(7): - The seconds of the minute
               * value(8): - The milliseconds of the second

EXAMPLE
  Sample program:

    program demo_time_and_date
    implicit none
    character(len=8)     :: date
    character(len=10)    :: time
    character(len=5)     :: zone
    integer,dimension(8) :: values
        call date_and_time(date,time,zone,values)
        ! using keyword arguments
        call date_and_time(DATE=date,TIME=time,ZONE=zone)
        call date_and_time(VALUES=values)
        print '(*(g0))','DATE="',date,'" TIME="',time,'" ZONE="',zone,'"'
	write(*,'(i5,a)') &
         & values(1),' - The year', &
         & values(2),' - The month', &
         & values(3),' - The day of the month', &
         & values(4),' - Time difference with UTC in minutes', &
         & values(5),' - The hour of the day', &
         & values(6),' - The minutes of the hour', &
         & values(7),' - The seconds of the minute', &
         & values(8),' - The milliseconds of the second'
    end program demo_time_and_date

  Results:

   DATE="20201222" TIME="165738.779" ZONE="-0500"
    2020 - The year
      12 - The month
      22 - The day of the month
    -300 - Time difference with UTC in minutes
      16 - The hour of the day
      57 - The minutes of the hour
      38 - The seconds of the minute
     779 - The milliseconds of the second

STANDARD
   [[Fortran 95]] and later

SEE ALSO
   cpu_time(3), system_clock(3)
 JSU
