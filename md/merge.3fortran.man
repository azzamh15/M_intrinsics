NAME
   merge(3f) - [FORTRAN:INTRINSIC:ARRAY CONSTRUCTION] Merge variables

SYNTAX
     result = merge(tsource, fsource, mask)

DESCRIPTION
   Select values from two arrays or scalars according to a logical
   mask. The result is equal to TSOURCE if MASK is .true., or equal to
   FSOURCE if it is .false. .

   Multi-dimensional arrays are supported.

ARGUMENTS
   TSOURCE    May be of any type.
   FSOURCE    Shall be of the same type and type parameters as TSOURCE.
   MASK       Shall be of type LOGICAL.

RETURN VALUE
   The result is of the same type and type parameters as TSOURCE. For any
   element the result is TSOURCE if MASK is true and FSOURCE otherwise.

EXAMPLES
  The value of

   merge (1.0, 0.0, k > 0)

  is 1.0 for K=5 and 0.0 for K=-2.

  Note that (currently) CHARACTER values must be of the same length.

   program demo_merge
   implicit none
   integer :: tvals(2,3), fvals(2,3), answer(2,3)
   logical :: mask(2,3)
   integer :: i

      tvals(1,:)=[  10, -60,  50 ]
      tvals(2,:)=[ -20,  40, -60 ]

      fvals(1,:)=[ 0, 3, 2 ]
      fvals(2,:)=[ 7, 4, 8 ]

      mask(1,:)=[ .true.,  .false., .true. ]
      mask(2,:)=[ .false., .false., .true. ]

      write(*,*)'mask of logicals'
      answer=merge( tvals, fvals, mask )
      write(*, '(3i4)')(answer(i, :), i=1, size(answer, dim=1))

      write(*, *)'highest values'
      answer=merge( tvals, fvals, tvals > fvals )
      write(*, '(3i4)')(answer(i, :), i=1, size(answer, dim=1))

      write(*, *)'lowest values'
      answer=merge( tvals, fvals, tvals < fvals )
      write(*, '(3i4)')(answer(i, :), i=1, size(answer, dim=1))

   end program demo_merge

  Expected values:

    mask of logicals
     10   3  50
      7   4 -60
    highest values
     10   3  50
      7  40   8
    lowest values
      0 -60   2
    -20   4 -60

STANDARD
   [[Fortran 95]] and later
CLASS
   [[Elemental procedure|Elemental function]]
