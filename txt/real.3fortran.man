NAME

REAL(3) - [TYPE:NUMERIC] Convert to real type

SYNTAX

    result = real(x, kind)

DESCRIPTION

REAL(X, KIND) converts its argument X to a real type.

For complex values this is similar to the modern complex-part-designator
%RE which also designates the real part of a value, accept a designator
can appear on the left-hand side of an assignment as well, as in
VAL%RE=(3.0,4.0).

ARGUMENTS

-   X : Shall be integer, real, or complex to convert to real.

-   KIND : (Optional) An integer initialization expression indicating
    the kind parameter of the result.

RETURNS

These functions return a real variable or array under the following
rules:

1.  REAL(x) is converted to a default real type if X is an integer or
    real variable.

2.  REAL(x) is converted to a real type with the magnitude of the real
    component of a complex value with kind type parameter of X.

3.  REAL(X, KIND) is converted to a real type with kind type parameter
    KIND if X is a complex, integer, or real variable.

EXAMPLES

Sample program:

    program demo_real
    use,intrinsic :: iso_fortran_env, only : dp=>real64
    implicit none
    complex              :: zr = (1.0, 2.0)
    doubleprecision      :: xd=huge(3.0d0)
    complex(kind=dp) :: zd=cmplx(4.0e0_dp,5.0e0_dp,kind=dp)

       print *, real(zr), aimag(zr)
       print *, dble(zd), aimag(zd)

       write(*,*)xd,real(xd,kind=kind(0.0d0)),dble(xd)
    end program demo_real

Results:

     1.00000000       2.00000000
     4.0000000000000000       5.0000000000000000
     1.7976931348623157E+308  1.7976931348623157E+308  1.7976931348623157E+308

STANDARD

FORTRAN 77 and later

SEE ALSO

DBLE(3), CMPLX(3), AIMAG(3), INT(3)

fortran-lang intrinsic descriptions
