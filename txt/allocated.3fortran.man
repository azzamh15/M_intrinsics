NAME

ALLOCATED(3) - [ARRAY INQUIRY] Status of an allocatable entity

SYNTAX

      result = allocated(array)

or

      result = allocated(scalar)

DESCRIPTION

ALLOCATED(ARRAY) and ALLOCATED(SCALAR) check the allocation status of
ARRAY and SCALAR, respectively.

ARGUMENTS

-   ARRAY : the argument shall be an allocatable array or allocatable
    scalar.

-   SCALAR : the argument shall be an allocatable scalar.

RETURNS

The return value is a scalar logical with the default logical kind type
parameter. If the argument is allocated then the result is .true.;
otherwise, it returns .false..

EXAMPLES

Sample program:

    program demo_allocated
    use,intrinsic :: iso_fortran_env, only : dp=>real64,sp=>real32
    implicit none
    integer :: i = 4
    real(kind=sp), allocatable :: x(:)
    character(len=256) :: message
    integer :: istat

       ! if already allocated, deallocate
       if ( allocated(x) ) deallocate(x,STAT=istat, ERRMSG=message )
       if(istat.ne.0)then
          write(*,*)trim(message)
          stop
       endif

       ! only if not allocated, allocate
       if ( .not. allocated(x) ) allocate(x(i))

       write(*,*)allocated(x), size(x)
       if( allocated(x)) then
           write(*,*)'do things if allocated'
       else
           write(*,*)'do things if not allocated'
       endif

       call intentout(x)
       write(*,*)'note it is deallocated!',allocated(x)

       contains

       subroutine intentout(arr)
       ! note that if arr has intent(out) and is allocatable,
       ! arr is deallocated on entry
       real(kind=sp),intent(out),allocatable :: arr(:)
           write(*,*)'note it was allocated in calling program',allocated(arr)
       end subroutine intentout

    end program demo_allocated

Results:

        T           4
        do things if allocated
        note it was allocated in calling program F
        note it is deallocated! F

STANDARD

Fortran 95 and later. Note, the scalar= keyword and allocatable scalar
entities are available in Fortran 2003 and later.

SEE ALSO

MOVE_ALLOC(3)

fortran-lang intrinsic descriptions SYNOPSIS
